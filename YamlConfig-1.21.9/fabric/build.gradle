plugins {
    id "com.github.johnrengelman.shadow"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${project.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    shadowCommon "org.snakeyaml:snakeyaml-engine:2.7"
    modImplementation "curse.maven:ui-933200:${project.ui_library_file_fabric}"
}

processResources {
    var replaceProperties = [
            "version": project.version,
            "mod_id": project.mod_id,
            "mod_name": project.mod_name,
            "mod_description": project.mod_description,
            "mod_author": project.mod_author,
            "mod_license": project.mod_license,
            "mod_website": project.mod_website,
            "mod_repository": project.mod_repository,

            "minecraft_version": project.minecraft_version,
            "architectury_version": project.architectury_version,
            "ui_library_version": project.ui_library_version
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json']) {
        expand replaceProperties
    }
}

shadowJar {
    exclude "architectury.common.json"

    relocate "org.snakeyaml", "com.daqem.snakeyaml"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "fabric-dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "fabric"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    archiveClassifier = "fabric-sources"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_API_KEY")
    project {
        id = project.curse_forge_project_id
        releaseType = project.release_type
        changelogType = "markdown"
        changelog = rootProject.file('changelog.md')

        project.supported_minecraft_versions.split(',').each { version ->
            addGameVersion version
        }

        addGameVersion "Java 21"
        addGameVersion "Fabric"

        relations {
            project.curseforge_dependencies.split(',').each { dep ->
                requiredDependency dep.trim()
            }
        }

        mainArtifact(remapJar) {
            displayName = "$project.mod_name Fabric $project.minecraft_version - $project.mod_version"
        }

        addArtifact(remapSourcesJar) {
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_API_KEY")
    projectId = project.modrinth_project_id
    versionName = "$project.mod_name Fabric $project.minecraft_version - $project.mod_version"
    versionNumber = "$project.mod_version"
    versionType = "$project.release_type"
    uploadFile = remapJar
    additionalFiles = [remapSourcesJar]
    gameVersions = project.supported_minecraft_versions.split(',').toList()
    loaders = ["fabric"]
    changelog = rootProject.file('changelog.md').text
    dependencies {
        project.modrinth_dependencies.split(',').each { dep ->
            required.project dep
        }
    }
}
